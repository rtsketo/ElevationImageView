import static org.gradle.api.JavaVersion.VERSION_21

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

ext {
    publishedGroupId = 'com.qhutch.elevationimageview'
    artifact = 'elevationimageview'
    libraryVersion = '2.5'
    libraryDescription = 'Imageview with elevation shadow'
    siteUrl = 'https://github.com/qhutch/ElevationImageView'
    gitUrl = 'https://github.com/qhutch/ElevationImageView.git'
    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
    developerId = 'qhutch'
    developerName = 'Quentin Menini'
    developerEmail = 'quentin.menini@gmail.com'
}

android {
    namespace 'com.qhutch.elevationimageview'

    compileSdkVersion sdkVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion sdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility VERSION_21
        targetCompatibility VERSION_21
    }

    kotlinOptions {
        jvmTarget = '21'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlin_version"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = publishedGroupId
            artifactId = artifact
            version = libraryVersion

            afterEvaluate {
                from components.release
            }

            pom {
                name = artifact
                description = libraryDescription
                url = siteUrl

                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }

                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }

                scm {
                    connection = "scm:git:$gitUrl"
                    developerConnection = "scm:git:$gitUrl"
                    url = siteUrl
                }
            }
        }
    }
}
